******
*
*  Format is:  EXECUPDT fn <ft   <fm   >> <(options...<)>>
*                           EXEC  *
*  Where:
*
*  fn ft fm
*        is the file identifier of the source input file.  The
*        file must consist of card image records with length
*        less than or equal to 255.  Record format must be
*        Fixed.
*        The last 8 columns of each card image must contain
*        a sequence number.
*        If the filetype or filemode are omitted,
*        EXEC and * are assumed, respectively.
*
* Options:
*
*    CTL cfn  specifies that "cfn CNTRL" is an update control
*             file that controls the application of multiple
*             update files to the source input file.
*
*    NOUPdate specifies that no update files are to be
*             applied to the source.  The HISTORY option
*             will be ignored if this is specified.
*
*    HISTory  specifies that the file 'cfn UPDATES' which is
*             created by the UPDATE command is to be appended
*             to the updated source as a comment.
*
*    NOHISTory specifies that no history log is to be appended
*             to the updated source.  This is the default.
*
*    SID      specifies that the last 17 columns contain
*             information to be removed from the source file.
*             This must be in the format
*
*                  n-16            n ( <= 255 )
*                  |               |
*                  V               V
*                  SID_CODE SEQ_NUMB
*
*    NOSID    specifies that only sequence numbers are to be
*             removed from the updated source (the last 8
*             columns).  This is the default.
*
*     Other options as with the UPDATE command.
*
******
&CONTROL OFF
 
&IX = 1
&PAR = &INDEX + 1
&LOOP -PARSCN &IX GT &INDEX
   &IF &&IX NE ( &SKIP 2
      &PAR = &IX
      &GOTO -PAREND
   &IX = &IX + 1
-PARSCN &CONTINUE
-PAREND &CONTINUE
 
&IF &PAR GT 1 &SKIP 2
   &TYPE ERROR: Target file name token missing
   &EXIT 12
 
&FN = &1
&IF &PAR LE 2 &SKIP 2
   &FT = &2
   &SKIP 1
   &FT = EXEC
&IF &PAR LE 3 &SKIP 2
   &FM = &SUBSTR &3 1 1
   &SKIP 1
   &FM = *
 
&IF &PAR LE 4 &SKIP 1
   &TYPE Warning: Extra tokens starting with ' &4 ' ignored
 
&IX = &PAR + 1
&CFN   =
&HIST  = 0
&NOUPD = 0
&SID   = 0
&UCNT = 0
 
&LOOP -OPTLP &IX GT &INDEX
   &OPT = &&IX
   &LEN = &LENGTH &OPT
   &IF /&OPT NE /CTL &SKIP 4
      &IX = &IX + 1
      &CFN = &&IX
      &NOUPD = 0
      &GOTO -OPTNX
   &IF /&OPT NE /SID &SKIP 2
      &SID = 1
      &GOTO -OPTNX
   &IF /&OPT NE /NOSID &SKIP 2
      &SID = 0
      &GOTO -OPTNX
   &IF &LEN LT 4 &SKIP 4
   &TST = &SUBSTR NOUPDATE 1 &LEN
   &IF /&OPT NE /&TST  &SKIP 2
      &NOUPD = 1
      &GOTO -OPTNX
   &IF &LEN LT 4 &SKIP 4
   &TST = &SUBSTR HISTORY 1 &LEN
   &IF /&OPT NE /&TST  &SKIP 2
      &HIST = 1
      &GOTO -OPTNX
   &IF &LEN LT 6 &SKIP 4
   &TST = &SUBSTR NOHISTORY 1 &LEN
   &IF /&OPT NE /&TST &SKIP 2
      &HIST = 0
      &GOTO -OPTNX
   &UCNT = &UCNT + 1
   &U&UCNT = &OPT
-OPTNX &CONTINUE
   &IX = &IX + 1
-OPTLP &CONTINUE
 
&IF &NOUPD EQ 0 &GOTO -NUPDSKP
   &IF /&CFN EQ / &SKIP 2
      &CFN =
      &TYPE Warning: NOUPDATE option cancels CTL option.
      &TYPE No updates will be applied.
   &IF &HIST EQ 0 &SKIP 2
      &HIST = 0
      &TYPE Warning: NOUPDATE option cancels HISTORY option.
-NUPDSKP &CONTINUE
 
&SFT = $&FT
MAKEBUF
LISTFILE &FN &SFT &FM ( ALL FIFO
&IF &RETCODE EQ 0 &GOTO -SETFM
   &REALRC = &RETCODE
   DROPBUF
   &IF &REALRC NE 28 &SKIP 1
      &TYPE Error: Cannot find source for ' &FN &FT &FM '
* Otherwise, LISTFILE issues error
   &EXIT &REALRC
 
-SETFM &CONTINUE
&READ VARS &X &X &FM &X &LRECL &X
DROPBUF
 
* Last choice target
&WORKFM = A
 
MAKEBUF
&TST = &SUBSTR &FM 1 1
QUERY DISK &TST ( LIFO
&IF &RETCODE EQ 0 &SKIP 3
   &REALRC = &RETCODE
   DROPBUF
   &EXIT &REALRC
 
&READ VARS &X &X &MODE &STAT &X
&IF &STAT NE R/W &SKIP 2
   &WORKFM = &SUBSTR &MODE 1 1
   &GOTO -WFMSET
&PARENT = &SUBSTR &MODE 3 1
QUERY DISK &PARENT ( LIFO
&IF &RETCODE EQ 0 &SKIP 3
   &REALRC = &RETCODE
   DROPBUF
   &EXIT &REALRC
&READ VARS &X &X &X &STAT &X
&IF &STAT NE R/W &SKIP 1
   &WORKFM = &PARENT
-WFMSET &CONTINUE
DROPBUF
 
&UPDRC = 0
&IF &NOUPD NE 1 &GOTO -DOUPD
   &SFN = &FN
   &SFM = &SUBSTR &FM 1 1
   &GOTO -COPY
-DOUPD &CONTINUE
   &SFN = $&FN
   &SFM = &WORKFM
 
   &ARGS &U1 &U2 &U3 &U4 &U5 &U6 &U7 &U8 &U9 &U10
 
   &IF /&CFN EQ / &SKIP 2
      UPDATE &FN &SFT &FM &CFN CNTRL * ( CTL &1 &2 &3 &4 &5 &6 &7 &8 &9 &10
   &SKIP 1
      UPDATE &FN &SFT &FM ( &1 &2 &3 &4 &5 &6 &7 &8 &9 &10
   &UPDRC = &RETCODE
   &IF &RETCODE EQ 0 &GOTO -COPY
      &IF &RETCODE NE 40 &SKIP 3
         &SFN = &FN
         &SFM = &SUBSTR &FM 1 1
         &GOTO -COPY
      &IF &RETCODE NE 12 &EXIT &RETCODE
-COPY &CONTINUE
 
&TRUNC = 8
&IF &SID EQ 1 &TRUNC = 17
&LEN = &LRECL - &TRUNC
COPYFILE &SFN &SFT &SFM &FN &FT &WORKFM ( REP RECFM V LRECL &LEN TRUNC
 
&IF &HIST EQ 0 &GOTO -NOHIST
&IF &NOUPD EQ 1 &GOTO -NOHIST
&IF &UPDRC EQ 40 &GOTO -NOHIST
 
   &BCMT =
   &ECMT =
   &LCMT = *
   MAKEBUF
   EXECIO 1 DISKR &FN &FT &WORKFM ( LIFO
   &READ VARS &X &Z
   &LEN = &LENGTH &X
   &IF &LEN GT 2 &X = &SUBSTR &X 1 2
   &IF /&X NE //* &SKIP 3
      &BCMT = /*
      &ECMT = */
      &GOTO -DOHIST
   &IF /&X NE //* &SKIP 1
      &LCMT = //
 
-DOHIST &CONTINUE
   DROPBUF
 
   MAKEBUF
   LISTFILE &FN &FT &WORKFM ( LABEL NOH LIFO
   &READ VARS &X &X &X &X &X &NR &X &DT &TM &X
   DROPBUF
 
   MAKEBUF
   &STACK FIFO TOP
   &IF /&BCMT EQ / &SKIP 1
      &STACK FIFO INPUT &BCMT
 
   &STACK FIFO INPUT &LCMT ******** ******** ******** ******** ******** ********
   &STACK FIFO INPUT &LCMT * UPDATES applied on &DT at &TM
   &STACK FIFO INPUT &LCMT ******** ******** ******** ******** ******** ********
   &STACK FIFO INPUT &LCMT *
   &STACK FIFO CHANGE ??&LCMT * ? *
   &IF /&ECMT EQ / &SKIP 2
      &STACK FIFO BOT
      &STACK FIFO INPUT &ECMT
   &STACK FIFO RECFM V
   &STACK FIFO FILE
 
   &STACK LIFO HT
   EDIT &FN UPDATES &WORKFM ( NODISP
   &STACK LIFO RT
   DROPBUF
 
   MAKEBUF
   &STACK FIFO BOTTOM
   &STACK FIFO GET &FN UPDATES &WORKFM
   &STACK FIFO FILE
   &STACK LIFO HT
   EDIT &FN &FT &WORKFM ( NODISP
   &STACK LIFO RT
   DROPBUF
 
   ERASE &FN UPDATES &WORKFM
 
-NOHIST &CONTINUE
 
&IF &SFN EQ $&FN ERASE $&FN $&FT &WORKFM
 
&EXIT &UPDRC
